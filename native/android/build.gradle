// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext {
        buildToolsVersion = "30.0.2"
        minSdkVersion = 21
        compileSdkVersion = 30
        targetSdkVersion = 30
        ndkVersion = "21.4.7075529"
        // stick to browser 1.3.0 until we upgrade to SDK 31 https://stackoverflow.com/questions/69208613/getting-error-while-running-react-native-app
        androidXBrowser = "1.3.0"

        // Config for MapLibre
        rnmbglMapboxLibs = {
            implementation("org.maplibre.gl:android-sdk:9.2.1")
            implementation ("com.mapbox.mapboxsdk:mapbox-sdk-turf:5.8.0")
        }
        rnmbglMapboxPlugins = {
            implementation("com.mapbox.mapboxsdk:mapbox-android-gestures:0.7.0")
            implementation("com.mapbox.mapboxsdk:mapbox-android-plugin-localization-v9:0.12.0") {
                exclude group: 'com.mapbox.mapboxsdk', module: 'mapbox-android-sdk'
            }
            implementation("com.mapbox.mapboxsdk:mapbox-android-plugin-annotation-v9:0.8.0") {
                exclude group: 'com.mapbox.mapboxsdk', module: 'mapbox-android-sdk'
            }
            implementation("com.mapbox.mapboxsdk:mapbox-android-plugin-markerview-v9:0.4.0") {
                exclude group: 'com.mapbox.mapboxsdk', module: 'mapbox-android-sdk'
            }
        }
    }
    repositories {
        google()
        mavenCentral()
        jcenter()
        maven {
            url = "https://dl.bintray.com/maplibre/maplibre-gl-native"
        }
    }
    dependencies {
        classpath("com.android.tools.build:gradle:4.2.2")

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        jcenter()
        maven {
            // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
            url("$rootDir/../node_modules/react-native/android")
        }
        maven {
            // Android JSC is installed from npm
            url("$rootDir/../node_modules/jsc-android/dist")
        }
        mavenCentral {
            // We don't want to fetch react-native from Maven Central as there are
            // older versions over there.
            content {
                excludeGroup "com.facebook.react"
            }
        }
        google()
        maven { url 'https://www.jitpack.io' }
    }
}

task downloadDependencies() {
    description 'Download all dependencies to the Gradle cache'
    doLast {
        configurations.findAll().each { config ->
            if (config.name.contains("minReactNative") && config.canBeResolved) {
                print config.name
                print '\n'
                config.files
            }
        }
    }
}

subprojects {
    afterEvaluate {
        if (getPlugins().hasPlugin('android') ||
                getPlugins().hasPlugin('android-library')) {

            configure(android.lintOptions) {
                abortOnError false
            }
        }
    }
}
